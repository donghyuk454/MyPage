plugins {
	id 'org.springframework.boot' version '2.6.7'
	id "org.sonarqube" version "3.0"
	id 'jacoco'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "org.asciidoctor.convert" version "1.5.8"
	id 'java'
}

group = 'com.mong'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.session:spring-session-data-redis'
	implementation group: 'com.github.ulisesbocchio', name: 'jasypt-spring-boot-starter', version: '3.0.4'
	implementation 'com.google.code.gson:gson:2.8.7'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'mysql:mysql-connector-java'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

jacoco {
	toolVersion = "0.8.7"
}

test {
	outputs.dir snippetsDir
	finalizedBy jacocoTestReport
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	outputDir file('build/docs/html5')
	dependsOn test
}

asciidoctor.doFirst {
	delete file('src/main/resources/static/docs')
}

task copyDocument(type: Copy) {
	dependsOn asciidoctor
	from "${asciidoctor.outputDir}"
	into file("src/main/resources/static/docs")
}

build {
	dependsOn copyDocument
}

bootJar {
	dependsOn asciidoctor
	from("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}

jacocoTestReport {
	finalizedBy jacocoTestCoverageVerification

	reports {
		csv.required = true
		xml.required = true
		html.required = true
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it,
					exclude: ["com/mong/project/exception/*",
							  "com/mong/project/dto/*",
							  'com.mong.project.util.readme.*'])
		}))
	}
}

jacocoTestCoverageVerification {
	def qDomains = []

	for (qPattern in "*.QA".."*.QZ") {
		qDomains.add(qPattern + "*")
	}

	violationRules {
		rule {
			element = 'CLASS'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.40
			}

			limit {
				counter = 'METHOD'
				value = 'COVEREDRATIO'
				minimum = 0.40
			}

			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.40
			}

			excludes = ['com.mong.project.MyProjectApplication',
						'com.mong.project.exception.*',
						'com.mong.project.dto.*',
						'com.mong.project.util.readme.*'] + qDomains
		}
	}
}
